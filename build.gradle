// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        kotlin_version = '1.8.0'
        lifecycle_version = '2.6.1'
        hilt_version = '2.45'
        markwon_version = '4.6.2'
        jlatexmath_version = '0.2.0'
        room_version = '2.5.1'
    }

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.4'
        classpath 'androidx.navigation:navigation-safe-args-gradle-plugin:2.5.3'
        classpath 'com.google.gms:google-services:4.3.15'
        classpath 'gradle.plugin.com.onesignal:onesignal-gradle-plugin:0.14.0'
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
    }
}

plugins {
    id "com.diffplug.spotless" version "6.17.0"
}

subprojects {
    apply plugin: "com.diffplug.spotless"

    spotless {
        kotlin {
            target("**/*.kt")
            targetExclude("$buildDir/**/*.kt")
            targetExclude("bin/**/*.kt")

            ktlint("0.48.2")
                    .editorConfigOverride([
                            "ktlint_code_style": "android"
                    ])
            licenseHeaderFile(rootProject.file("spotless/copyright.kt"))
        }
    }
}